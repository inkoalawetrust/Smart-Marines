//This code is taken largely from the old broken marine code base. Since this code DOES actually function.
Extend Class SM_Marine
{
	//The code related to the marine making the player die.
	Override String GetObituary (Actor Victim, Actor Inflictor, Name MOD, Bool PlayerAttack)
	{
		//Melee obituaries
		If (Inflictor == Self && MOD == 'Melee')
		{
			If (IsInState (Self,"Melee.Smack"))
			{
				If (HasBerserk)
					Return "%o got %p head turned into a baseball by a marine";
				Else
					Return HitObituary;
			}
			Else If (IsInState (Self,"Melee.Kick"))
			{
				If (HasBerserk)
					Return "%o got punted into orbit";
				Else
					Return "%o was neutered by a marines' foot";
			}
			Else If (IsInState (Self,"Melee.Jumpkick.Attack"))
			{
				If (HasBerserk)
					Return "%o got %p head turned into a football by a marine";
				Else
					Return "%o was dropkicked to death by a marine";
			}
			Else If (IsInState (Self,"Melee.OnePunch"))
				Return "%o found out what the marine was doing";

			Return HitObituary;
		}
		
		If (Inflictor Is "SM_FragGrenade")
		{
			If (MOD == 'Shrapnel')
				Return "%o couldn't outrun a grenades' shrapnel";
			Else If (MOD == "Grenade" || MOD == 'Explosive' || MOD == 'Explosion' || MOD == 'ExplosiveImpact')
				Return "%o was blown up by a marine";
		}
		If (Inflictor Is "SM_ShrapnelPuff")
			If (MOD == 'Shrapnel')
				Return "%o couldn't outrun a grenades' shrapnel";
		
		//Special rifle obituary.
		If (Inflictor && Inflictor Is "SM_Bullet" && AttackType == FIRE_SINGLE && Random (True, False) == True)
			Return "%o was sniped by a marine";
		
		Return Super.GetObituary (Victim, Inflictor, MOD, PlayerAttack);
	}
	
	Override Void OnDestroy() //I'd consider this to be a form of death.
	{
		If (!bDormant && Random (0,128) == Random (0,128))
		{
			Tracer = Spawn ("SM_MarineErasedEffect",Pos);
			Tracer.Angle = Angle;
		}
		Super.OnDestroy();
	}
	
	Int DeathAnimPlayed; //Keeps track of what death animation the marine played, so it can be played in reverse when resurrected.
	
	States
	{
		Death: //Plays one of 5 different animations.
			TNT1 A 0 {If (!User_NoWeaponDrop) A_DropItem ("SM_Rifle",-1,48);}
			TNT1 A 0 A_JumpIf (Crouching,"CrouchDeath");
			TNT1 A 0 A_Jump (256,"DeathAnim1","DeathAnim2","DeathAnim3","DeathAnim4","DeathAnim5");
		DeathAnim1: //The vanilla player death.
			MRDV H 5 {DeathAnimPlayed = 1;}
			MRDV I 5 A_ScreamAndUnblock();
			MRDV JKLM 5;
			MRDV N -1;
			Stop;
		DeathAnim2: //Alternate falling forward death.
			MARD A 5 {DeathAnimPlayed = 2;}
			MARD B 5 A_ScreamAndUnblock();
			MARD CDEF 5;
			MARD G 35 A_SetTics (35*Random (1,4)); //Wait a bit.
			MARD MN 8; //Head falls pointing down.
			MARD O -1;
			Stop;
		DeathAnim3: //Falling back death. 
			MARD H 5 {DeathAnimPlayed = 3;}
			MARD I 5 A_ScreamAndUnblock();
			MARD JK 5;
			MARD L -1;
			Stop;
		DeathAnim4: //Falling sideways death.
			MARD P 5 {DeathAnimPlayed = 4;}
			MARD Q 5 A_ScreamAndUnblock();
			MARD RSTUVW 5;
			MARD X -1;
			Stop;
		DeathAnim5: //Alternate falling sideways death.
			MARE A 5 {DeathAnimPlayed = 5;}
			MARE B 5 A_ScreamAndUnblock();
			MARE CDEF 5;
			MARE G -1;
			Stop;
		CrouchDeath:
			MARP G 5
			{
				Crouching = False;
				Height = Default.Height;
				DeathAnimPlayed = 6;
			}
			MARP H 5 A_ScreamAndUnblock();
			MARP IJ 5;
			MARP K -1;
			Stop;
		XDeath: //Mega oof
			TNT1 A 0 {If (!User_NoWeaponDrop) A_DropItem ("SM_Rifle",-1,48);}
			TNT1 A 0 A_JumpIf (Crouching,"CrouchXDeath");
			MRDV O 5;
			MRDV P 5 A_StartSound("Marine/XDeath",CHAN_VOICE);
			MRDV Q 5 A_NoBlocking();
			MRDV RSTUV 5;
			MRDV W -1;
			Stop;
		CrouchXDeath:
			MARP L 5
			{
				Crouching = False;
				Height = Default.Height;
			}
			MARP M 5 A_StartSound("Marine/XDeath",CHAN_VOICE);
			MARP N 5 A_NoBlocking();
			MARP OPQRS 5;
			MARP T -1;
			Stop;
		Raise:
			TNT1 A 0
			{
				//I could shorten this using String.Format(), if strings/names could even be converted to state labels that is.
				Switch (DeathAnimPlayed)
				{
					Case 0:
						Return ResolveState ("RaiseAnim1");
					Case 1:
						Return ResolveState ("RaiseAnim1");
					Case 2:
						Return ResolveState ("RaiseAnim2");
					Case 3:
						Return ResolveState ("RaiseAnim3");
					Case 4:
						Return ResolveState ("RaiseAnim4");
					Case 5:
						Return ResolveState ("RaiseAnim5");
					Case 6:
						Return ResolveState ("CrouchRaise");
				}
				
				Return ResolveState (Null);
			}
			Goto See;
		RaiseAnim1:
			MRDV NMLKJIH 5;
			Goto See;
		RaiseAnim2:
			MARD ONMGFEDCBA 5;
			Goto See;
		RaiseAnim3:
			MARD LKJIH 5;
			Goto See;
		RaiseAnim4:
			MARD XWVUTSRQP 5;
			Goto See;
		RaiseAnim5:
			MARE GFEDCBA 5;
			Goto See;
		CrouchRaise:
			MARP KJIHG 5;
			Goto See;
		GenericFreezeDeath:
			MARA E 0;
			#### # 5 A_GenericFreezeDeath;
			---- A 1 A_FreezeDeathChunks;
			Wait;
	}
}