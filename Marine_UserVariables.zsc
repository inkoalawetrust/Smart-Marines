//Code related to the marines' user variables goes here.

Extend Class SmartMarine
{
	Override Void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		//The //$UserDefaultValue editor key is buggy, so this code still needs to be used.
		If (User_SearchTime == 0) User_SearchTime = 300; //The amount of time the marine tries to chase it's target, after it has died or gone out of sight.
		If (User_GrenadeThreshold == 0) User_GrenadeThreshold = 240; //Default threshold that needs to be reached or surpassed, for the marine to throw a grenade.
		If (User_RetreatAttempts == 0) User_RetreatAttempts = 10; //Default amount of attempts the marine makes to run for cover to reload.
		If (User_AlertRange == 0) User_AlertRange = 384; //Default range in which marines alert each other of enemies.
		If (User_EnemyAlertHearingRange == 0) User_EnemyAlertHearingRange = 1024; //Default range in which marines can hear the alerts of enemy marines.
		If (User_DodgeRange == 0) User_DodgeRange = 384; //Default range in which marines dodge projectiles.
		If (User_MeleeChance == 0) User_MeleeChance = 96; //Default chance for the marine to try running away after hitting something with his rifles' stock.
		If (User_TurretThreshold == 0) User_TurretThreshold = 100; //Default chance for the marine to use a turret.
		If (User_FearDistance == 0) User_FearDistance = 2048; //The default distance the marines keeps from powerful enemies.
		
		HandleRandomPersonalityMidGame();
		HandleMarineColoring();
	}
	
	//Handles the coloring of the marines.
	Void HandleMarineColoring()
	{
		If (!User_Color || User_Color ~== "") Return; //Don't run the code if User_Color is empty.
		
		//Handles the random color choice that is performed if the color name given is "Random".
		If (User_Color ~== "Random")
		{
			Static Const String ColorArray[] = {"Red","Gray","White","Black","Blue","Yellow","Orange","Pink","DarkGreen","Default"}; //Contains all the valid color names.
			
			User_Color = ColorArray[Random(0,9)]; //Randomly picks a color string.
		}

		A_SetTranslation (String.Format("Marine_%s", User_Color));
	}
	
	Void HandleRandomPersonalityMidGame()
	{
		If (User_RandomPersonality)
		{
			If (!User_SearchTime) User_SearchTime = 300*FRandom (-0.6,3.0);
			If (!User_GrenadeThreshold) User_GrenadeThreshold = 240*FRandom (-0.75,2.0);
			If (!User_RetreatAttempts) User_RetreatAttempts = Random (7,15);
			If (!User_FearDistance) User_FearDistance = 2048*FRandom (0.75,2.0);
			If (!User_TurretThreshold) User_TurretThreshold = 100*Frandom (0.5,1.5);
			If (!User_MeleeChance) User_MeleeChance = 96*FRandom (0.5,2.5);
			If (!User_Color || User_Color ~== "") User_Color = "Random";
			User_RandomPersonality = False;
		}
	}
	
	Int User_SearchTime;
	Int User_GrenadeThreshold;
	Int User_RetreatAttempts;
	Int User_TurretThreshold;
	UInt8 User_MeleeChance; //This is a UInt8 because it's plugged directly into A_Jump calls, which already don't support values beyond 0-255.
	Double User_AlertRange;
	Double User_EnemyAlertHearingRange;
	Double User_FearDistance;
	Double User_DodgeRange;
	Bool User_DisobeyCommands;
	Bool User_NoReload;
	Bool User_NoCover;
	Bool User_RandomPersonality;
	Bool User_NoRifleDrop;
	String User_Color;

} 