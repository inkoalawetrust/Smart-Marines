Version "4.5"

#Include "VanillaRifle.zsc" //The rifle the marine may drop when killed by an enemy.
#Include "Marine_OtherActors.zsc" //Other actors related to the marine. Like the grenades and empty magazines.

//To do:
/*HATE. LET ME TELL YOU HOW MUCH I'VE COME TO HATE YOU SINCE I BEGAN TO WORK ON YOU.
THERE ARE 86,000,000,000 NEURONS THAT MY BRAIN IS COMPOSED OF.
IF THE WORD HATE WAS ENGRAVED ON EACH NANOANGSTROM OF THOSE TENS OF BILLIONS OF NEURONS IT WOULD NOT EQUAL ONE ONE-BILLIONTH OF THE HATE I FEEL FOR
MARINES AT THIS MICRO-INSTANT FOR YOU. HATE. HATE.*/
//Fix the bug that causes hitscans fired by friendly marines to go through enemy marines.
//Maybe try simplifying the code for the grenade projectile.

Class SmartMarine : Actor //Good joke, me, "smart".
{
	Default
	{
		//$Title AI Marine
		//$Category Marines
		//$Sprite MARIC1
		Health 120;
		GibHealth 20;
		Height 56;
		DeathHeight 12;
		CameraHeight 50;
		Radius 16;
		YScale 0.98;
		Mass 100;
		Speed 12;
		FastSpeed 24;
		PainChance 96;
		PainThreshold 5;
		FriendlySeeBlocks 20;
		MaxDropOffHeight 48;
		MaxTargetRange 8192;
		MinMissileChance 100;
		Tag "Marine NPC";
		Species "SmartMarine";
		Decal "BulletChip";
		Obituary "%o was shot to death by a marine";
		HitObituary "%o got %p head smashed in by a marines' rifle";
		DropItem "Clip";
		DropItem "Clip", 128, 2;
		DropItem "ClipBox", 64;
		DropItem "VanillaRifle", 48;
		DamageFactor "MarineHMG", 2.0;
		PainSound "Marine/pain";
		DeathSound "Marine/Death";
		Monster;
		+JumpDown;
		+MissileMore;
		+MissileEvenMore;
		+Telestomp;
		+NoInfightSpecies;
		+FloorClip;
		+AvoidMelee;
		+AvoidHazards; //This ONLY works for crushing ceilings and NOTHING ELSE. So it's largely useless but I'm still adding it.
	}
	
	Int DeathAnimPlayed; //Keeps track of what death animation the marine played, so it can be played in reverse when resurrected.
	
	States
	{
		Spawn:
			MARI ABACDC Random (25,35) A_LookEx (maxseedist:8192,3072,160);
			Loop;
		See:
			MAR2 AABBCCDD 4 A_Chase ("RifleSmack","RifleBurst");
			Loop;
		RifleSmack:
			MARM A 10;
			MARM B 8 A_CustomMeleeAttack (Random(4,8),"Marine/Melee","Marine/MeleeMiss",'Melee',False); //Blunt force attack, so no blood.
			Goto See;
		RifleBurst:
			MAR2 A 1;
			MARR A 4 Light ("MarineMuzzleFlash")
			{
				A_CustomBulletAttack (2.5,2.6,1,Random(6,12),"SmartMarinePuff",8196+2,CBAF_NORANDOM|CBAF_AIMFACING,spawnheight:42,-6);
				A_StartSound ("Marine/Fire",CHAN_WEAPON);
			}
			MAR2 A 1 A_MonsterRefire (64,"See");
			Loop;
		Pain:
			PLAY G 12 A_Pain();
			Goto See;
		Death: //Plays one of 4 different animations.
			TNT1 A 0 A_Jump (256,"DeathAnim1","DeathAnim2","DeathAnim3","DeathAnim4");
		DeathAnim1: //The vanilla player death.
			PLAY H 5 {DeathAnimPlayed = 1;}
			PLAY I 5 A_ScreamAndUnblock();
			PLAY JKLM 5;
			PLAY N -1;
			Stop;
		DeathAnim2: //Alternate falling forward death.
			MARD A 5 {DeathAnimPlayed = 2;}
			MARD B 5 A_ScreamAndUnblock();
			MARD CDEF 5;
			MARD G 35 A_SetTics (35*Random (1,4)); //Wait a bit.
			MARD MN 8; //Head falls pointing down.
			MARD O -1;
			Stop;
		DeathAnim3: //Falling back death. 
			MARD H 5  {DeathAnimPlayed = 3;}
			MARD I 5 A_ScreamAndUnblock();
			MARD JK 5;
			MARD L -1;
			Stop;
		DeathAnim4: //Falling sideways death.
			MARD P 5  {DeathAnimPlayed = 4;}
			MARD Q 5 A_ScreamAndUnblock();
			MARD RSTUVW 5;
			MARD X -1;
		XDeath: //Mega oof
			PLAY O 5;
			PLAY P 5 A_StartSound("Marine/XDeath",CHAN_VOICE);
			PLAY Q 5 A_NoBlocking();
			PLAY RSTUV 5;
			PLAY W -1;
			Stop;
		Raise:
			TNT1 A 0
			{
				//I could shorter this using String.Format(), if strings/names could even be converted to state labels that is.
				If (DeathAnimPlayed == 0 || DeathAnimPlayed == 1)
				{Return ResolveState ("RaiseAnim1");}
				Else If (DeathAnimPlayed == 2)
				{Return ResolveState ("RaiseAnim2");}
				Else If (DeathAnimPlayed == 3)
				{Return ResolveState ("RaiseAnim3");}
				Else If (DeathAnimPlayed == 4)
				{Return ResolveState ("RaiseAnim4");}
				Return ResolveState (Null);
			}
			Goto See;
		RaiseAnim1:
			PLAY NMLKJIH 5;
			Goto See;
		RaiseAnim2:
			MARD ONMGFEDCBA 5;
			Goto See;
		RaiseAnim3:
			MARD LKJIH 5;
			Goto See;
		RaiseAnim4:
			MARD XWVUTSRQP 5;
			Goto See;
	}
}